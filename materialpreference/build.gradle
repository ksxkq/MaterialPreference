apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:25.1.0'
}

group = GROUPID // library库包名
version = PUBLISHVERSION // 版本号

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}


task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator)) //使用android.getBootClasspath关联Android SDK类文件
}

task generateJavadocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavadocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavadocsJar //原文为generateJavaDocsJar，此处已做矫正，要严格显示大小写
    archives generateSourcesJar
}

bintray {
    user = USER //在个人中心的路径里可以找到
    key = APIKEY //上述保存的API key
    pkg {
        repo = 'maven' //必须设定一个值，这里选择maven
        name = PKGNAME //必须，包名
        version {
            name = PUBLISHVERSION //版本名称
            desc = DESC //版本描述
            released = new Date() //上传过程中如果此处报错，可以注释掉此句
            vcsTag = PUBLISHVERSION //版本
        }
        licenses = ['Apache-2.0'] //开源协议
        vcsUrl = GITURL //指定项目git地址
        websiteUrl = WEBSITE //项目官网地址
    }
    configurations = ['archives'] //必须为archives
}

